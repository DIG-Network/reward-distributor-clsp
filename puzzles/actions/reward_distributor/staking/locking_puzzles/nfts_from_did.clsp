; nfts_from_did.clsp by yakuhito
;; Allows any NFTs minted by a preset DID to be staked. All weights are equal (1).

;; This is a staking puzzle - output is (new_active_shares . output_conditions).
;; Ephemeral_State & Entry_Custody_Puzzle_Hash are Truths verified and passed by 'stake.clsp'.

(mod (@ curried_args_and_solution (
    (@ DID_SINGLETON_STRUCT (SINGLETON_MOD_HASH DID_LAUNCHER_ID . SINGLETON_LAUNCHER_HASH))
    NFT_STATE_LAYER_MOD_HASH
    NFT_OWNERSHIP_LAYER_MOD_HASH
    OFFER_MOD_HASH
    NONCE_MOD_HASH
    MY_P2_PUZZLE_HASH
    Ephemeral_State
    Entry_Custody_Puzzle_Hash
    my_id
    nft_infos
))
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include curry.clib)

    (defun notarized_payment (ph)
        (list ph 1 (list ph))
    )

    (defun nft_lancher_id (did_coin_id (@ nft_launcher_proof ((proof_puzzle_hash . proof_amount) . nft_launcher_proof_rest)))
        (if nft_launcher_proof
            (coinid
                (nft_lancher_id did_coin_id nft_launcher_proof_rest)
                proof_puzzle_hash
                proof_amount
            )
            ; else
            did_coin_id
        )
    )

    (defun-inline nft_lancher_id_stager (((did_parent_info did_inner_ph did_amount) . nft_launcher_proof))
        (nft_lancher_id
            (coinid
                did_parent_info
                (curry_hashes SINGLETON_MOD_HASH
                    (sha256tree DID_SINGLETON_STRUCT)
                    did_inner_ph
                )
                did_amount
            )
            nft_launcher_proof
        )
    )

    (defun add_announcement_assert_and_create_security_condition (ann conditions)
        (c
            (list ASSERT_PUZZLE_ANNOUNCEMENT ann)
            (c
                (list CREATE_PUZZLE_ANNOUNCEMENT ann)
                conditions
            )
        )
    )

    (defun add_multiple_nfts (
            (@ curried_args_and_solution (
                (@ DID_SINGLETON_STRUCT (SINGLETON_MOD_HASH DID_LAUNCHER_ID . SINGLETON_LAUNCHER_HASH))
                NFT_STATE_LAYER_MOD_HASH
                NFT_OWNERSHIP_LAYER_MOD_HASH
                OFFER_MOD_HASH
                NONCE_MOD_HASH
                MY_P2_PUZZLE_HASH
                Ephemeral_State
                Entry_Custody_Puzzle_Hash
                my_id
                nft_infos
            ))
            (@
                nft_infos
                (
                    (
                        nft_metadata_hash
                        nft_metadata_updater_hash_hash 
                        nft_owner_hash
                        nft_transfer_porgram_hash .
                        nft_launcher_proof
                    ) .
                    nft_infos_rest
                )
            )
            shares_so_far
            conditions_so_far
        )
        (if nft_infos
            (add_multiple_nfts
                curried_args_and_solution
                nft_infos_rest
                (+ shares_so_far 1)
                (add_announcement_assert_and_create_security_condition
                    (sha256
                        (curry_hashes SINGLETON_MOD_HASH
                            (sha256tree
                                (c SINGLETON_MOD_HASH (c (nft_lancher_id_stager nft_launcher_proof) SINGLETON_LAUNCHER_HASH))
                            )
                            (curry_hashes NFT_STATE_LAYER_MOD_HASH
                                (sha256 1 NFT_STATE_LAYER_MOD_HASH)
                                nft_metadata_hash
                                nft_metadata_updater_hash_hash  
                                (curry_hashes NFT_OWNERSHIP_LAYER_MOD_HASH
                                    (sha256 1 NFT_OWNERSHIP_LAYER_MOD_HASH)
                                    nft_owner_hash
                                    nft_transfer_porgram_hash
                                    OFFER_MOD_HASH
                                )
                            )
                        ) ; NFT puzzle hash
                        (sha256tree 
                            (list
                                (sha256tree (c Ephemeral_State my_id)) ; nonce
                                (notarized_payment (curry_hashes NONCE_MOD_HASH
                                    (sha256 1 entry_custody_puzzle_hash)
                                    MY_P2_PUZZLE_HASH
                                ))
                            )
                        )
                    )
                    conditions_so_far
                )
            )
            ; else
            (c
                shares_so_far
                (c
                    (list ASSERT_MY_COIN_ID my_id)
                    conditions_so_far
                )
            )
        )
    )

    (add_multiple_nfts
        curried_args_and_solution
        nft_infos
        0
        ()
    )
)