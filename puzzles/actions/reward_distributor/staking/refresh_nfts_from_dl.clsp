; refresh_nfts_from_dl.clsp by yakuhito
;; Refreshes entries of *refreshable* NFT distributors. Anyone can call this action to update any
;;   entries where one or more NFTs now have a different number of associated shares than they did
;;   when they were initially staked.

;; Note: This puzzle was specifically optimized for DL-based NFTs. It assumes the locking puzzle is
;;   'nfts_from_dl.clsp' and the unlocking puzzle is 'nfts.clsp'.

(mod (@ curried_args_and_solution (
    (@ DL_SINGLETON_STRUCT (SINGLETON_MOD_HASH DL_LAUNCHER_ID . SINGLETON_LAUNCHER_HASH))
    NFT_STATE_LAYER_MOD_HASH
    NFT_OWNERSHIP_LAYER_MOD_HASH
    OFFER_MOD_HASH
    NONCE_MOD_HASH
    MY_P2_PUZZLE_HASH
    ENTRY_SLOT_1ST_CURRY_HASH ; after 1st curry
    MAX_SECONDS_OFFSET ; at most this amount of seconds can pass since last update
    ; /\ this prevents the reward distributor from 'stealing' earned rewards while not
    ;    dropping the remove mirror tx from the mempool after each block
    PRECISION
    (
        Ephemeral_State .
        (@
            Current_State
            (
                total_reserves
                active_shares
                (@ Reward_Info (cumulative_payout . remaining_rewards))
                (@ Round_Time_Info (last_update . epoch_end))
            )
        )
    ) ; Truth
    (
        ; DL-related data
        dl_root_hash
        dl_metadata_rest_hash_or_null
        dl_metadata_updater_hash_hash
        dl_inner_puzzle_hash

        (@ existing_slot_value (
            entry_custody_puzzle_hash 
            existing_slot_cumulative_payout .
            existing_slot_shares
        ))
        entry_payout_amount
        payout_rounding_error
        shares_delta
        new_nft_shares

        nft_parent_id
        nft_launcher_id
        nft_metadata_hash
        nft_metadata_updater_hash_hash
        nft_transfer_porgram_hash
        nft_inclusion_proof
    )
))
    (include condition_codes.clib)
    (include merkle_utils.clib)
    (include sha256tree.clib)
    (include curry.clib)
    (include slots.clib)

    (defun create_coin_1_with_hint (ph)
        (list CREATE_COIN ph 1 (list ph))
    )

    ; main
    (c
        (c
            Ephemeral_State ; new ephemeral state
            (list
                (- total_reserves entry_payout_amount)
                (+ active_shares shares_delta)
                (c cumulative_payout (+ remaining_rewards payout_rounding_error))
                Round_Time_Info
            ) ; new state
        )
        (if (all
            (=
                (* existing_slot_shares (- cumulative_payout existing_slot_cumulative_payout))
                (+ (* entry_payout_amount PRECISION) payout_rounding_error)
            )
            (> payout_rounding_error -1)
            (> PRECISION payout_rounding_error)
            (not (> existing_slot_shares entry_shares))
            (= 
                dl_root_hash
                (simplify_merkle_proof
                    (sha256tree (c nft_launcher_id new_nft_shares))
                    nft_inclusion_proof
                )
            )
        )
            (c
                ; make sure the reward info is up to date
                (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ last_update MAX_SECONDS_OFFSET))
                (c
                    ; assert DL root hash
                    (list ASSERT_PUZZLE_ANNOUNCEMENT (sha256
                        (curry_hashes SINGLETON_MOD_HASH
                            (sha256tree DL_SINGLETON_STRUCT)
                            (curry_hashes NFT_STATE_LAYER_MOD_HASH
                                (sha256 1 NFT_STATE_LAYER_MOD_HASH)
                                (sha256 2
                                    (sha256 1 dl_root_hash)
                                    (if dl_metadata_rest_hash_or_null
                                        dl_metadata_rest_hash_or_null
                                        ; else
                                        (sha256_one)
                                    )
                                ) ; metadata hash
                                dl_metadata_updater_hash_hash
                                dl_inner_puzzle_hash
                            )
                        )
                        '$'
                    ))
                    (c
                        ; consume slot
                        (spend_slot ENTRY_SLOT_1ST_CURRY_HASH
                            (sha256tree existing_slot_value)
                        )
                        (c
                            ; re-issue slot
                            (create_slot_with_hint ENTRY_SLOT_1ST_CURRY_HASH
                                (sha256tree
                                    (c 
                                        entry_custody_puzzle_hash
                                        (c
                                            existing_slot_cumulative_payout
                                            (+ existing_slot_shares shares_delta)
                                        )
                                    )
                                ) ; slot value = (payout_puzzle_hash initial_cumulative_payout . shares)
                                entry_custody_puzzle_hash
                            )
                            ; restake NFT
                            (list SEND_MESSAGE
                                23 ; sender puzzle hash, receiver coin id
                                (sha256tree (c ; this is the leading 'q . ' in the delegated puzzle
                                    1 
                                    (list
                                        (create_coin_1_with_hint
                                            (curry_hashes NONCE_MOD_HASH
                                                (sha256tree (c entry_custody_puzzle_hash new_nft_shares))
                                                MY_P2_PUZZLE_HASH
                                            ) ; new puzzle hash
                                        )
                                    )
                                )) ; message = delegated puzzle hash
                                (coinid
                                    nft_parent_id
                                    (curry_hashes SINGLETON_MOD_HASH
                                        (sha256tree
                                            (c SINGLETON_MOD_HASH (c nft_launcher_id SINGLETON_LAUNCHER_HASH))
                                        )
                                        (curry_hashes NFT_STATE_LAYER_MOD_HASH
                                            (sha256 1 NFT_STATE_LAYER_MOD_HASH)
                                            nft_metadata_hash
                                            nft_metadata_updater_hash_hash  
                                            (curry_hashes NFT_OWNERSHIP_LAYER_MOD_HASH
                                                (sha256 1 NFT_OWNERSHIP_LAYER_MOD_HASH)
                                                (sha256 1 nft_owner)
                                                nft_transfer_porgram_hash
                                                (curry_hashes NONCE_MOD_HASH
                                                    (sha256tree (c entry_custody_puzzle_hash (- new_nft_shares shares_delta)))
                                                    MY_P2_PUZZLE_HASH
                                                )
                                            )
                                        )
                                    ) ; NFT puzzle hash
                                    1
                                )
                            )
                        )
                    )
                )
            ) ; conditions
            ; else
            (x)
        )
    )
)
