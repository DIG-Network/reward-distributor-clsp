; nfts.clsp by yakuhito
;; Sends a given set of staked NFTs back to the user.

;; This is an unlocking puzzle - output is (unlocked_shares . output_conditions).
;; Ephemeral_State & Entry_Custody_Puzzle_Hash are Truths verified and passed by 'stake.clsp'.

(mod (@ curried_args_and_solution (
    SINGLETON_MOD_HASH
    SINGLETON_LAUNCHER_HASH
    NFT_STATE_LAYER_MOD_HASH
    NFT_OWNERSHIP_LAYER_MOD_HASH
    NONCE_MOD_HASH
    MY_P2_PUZZLE_HASH
    Ephemeral_State
    Entry_Custody_Puzzle_Hash .
    nfts_to_unlock
))
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include curry.clib)

    (defun remove_nfts (
        (@ curried_args_and_solution (
            SINGLETON_MOD_HASH
            SINGLETON_LAUNCHER_HASH
            NFT_STATE_LAYER_MOD_HASH
            NFT_OWNERSHIP_LAYER_MOD_HASH
            NONCE_MOD_HASH
            MY_P2_PUZZLE_HASH
            Ephemeral_State
            Entry_Custody_Puzzle_Hash
            ; nfts_to_unlock - not used
        ))
        (@ nfts_to_unlock (
            (
                nft_launcher_id
                nft_parent_id
                nft_metadata_hash
                nft_metadata_updater_hash_hash
                nft_owner
                nft_transfer_porgram_hash .
                nft_shares
            ) . remaining_nfts_to_unlock
        ))
        shares_so_far
        conditions_so_far
    )
        (if nfts_to_unlock
            (remove_nfts
                curried_args_and_solution
                remaining_nfts_to_unlock
                (+ shares_so_far nft_shares)
                (c
                    ; remove entry
                    (list SEND_MESSAGE
                        23 ; sender puzzle hash, receiver coin id
                        (sha256tree (c ; this is the leading 'q . ' in the delegated puzzle
                            1 
                            (list
                                (list
                                    CREATE_COIN
                                    Entry_Custody_Puzzle_Hash
                                    1
                                    (list Entry_Custody_Puzzle_Hash)
                                )
                            )
                        )) ; message = delegated puzzle hash
                        (coinid
                            nft_parent_id
                            (curry_hashes SINGLETON_MOD_HASH
                                (sha256tree
                                    (c SINGLETON_MOD_HASH (c nft_launcher_id SINGLETON_LAUNCHER_HASH))
                                )
                                (curry_hashes NFT_STATE_LAYER_MOD_HASH
                                    (sha256 1 NFT_STATE_LAYER_MOD_HASH)
                                    nft_metadata_hash
                                    nft_metadata_updater_hash_hash  
                                    (curry_hashes NFT_OWNERSHIP_LAYER_MOD_HASH
                                        (sha256 1 NFT_OWNERSHIP_LAYER_MOD_HASH)
                                        (sha256 1 nft_owner)
                                        nft_transfer_porgram_hash
                                        (curry_hashes NONCE_MOD_HASH
                                            (sha256tree (c Entry_Custody_Puzzle_Hash nft_shares))
                                            MY_P2_PUZZLE_HASH
                                        )
                                    )
                                )
                            ) ; NFT puzzle hash
                            1
                        )
                    )
                    (c
                        ; owner actually wants us to remove this entry
                        (list RECEIVE_MESSAGE
                            18 ; puzzle hash - puzzle hash
                            (concat 'u' nft_launcher_id) ; message
                            Entry_Custody_Puzzle_Hash ; sender
                        )
                        conditions_so_far
                    )
                )
            )
            ; else
            (c shares_so_far conditions_so_far)
        )
    )

    (remove_nfts
        curried_args_and_solution
        nfts_to_unlock
        0
        ()
    )
)