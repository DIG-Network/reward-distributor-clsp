; remove_entry.clsp by yakuhito
;; Stops rewarding a specific entry.

(mod (
    SINGLETON_MOD_HASH
    MANAGER_SINGLETON_STRUCT_HASH ; (sha256 SINGLETON_STRUCT) for manager singleton
    ENTRY_SLOT_1ST_CURRY_HASH ; after 1st curry
    MAX_SECONDS_OFFSET ; at most this amount of seconds can pass since last update
    ; /\ this prevents the reward distributor from 'stealing' earned rewards while not
    ;    dropping the remove mirror tx from the mempool after each block
    PRECISION
    (
        Ephemeral_State . ; not used
        (@
            Current_State
            (
                total_reserves
                active_shares .
                (
                    (@ Reward_Info (cumulative_payout . remaining_rewards))
                    (@ Round_Time_Info (last_update . epoch_end))
                )
            )
        )
    ) ; Truth
    (
        manager_singleton_inner_puzzle_hash
        entry_payout_amount
        payout_rounding_error
        entry_payout_puzzle_hash
        entry_initial_cumulative_payout .
        entry_shares
    )
)
    (include condition_codes.clib)
    (include curry.clib)
    (include slots.clib)

    (if (all
        (=
            (* (- cumulative_payout entry_initial_cumulative_payout) entry_shares)
            (+ (* entry_payout_amount PRECISION) payout_rounding_error)
        )
        (> payout_rounding_error -1)
        (> PRECISION payout_rounding_error)
    )
        (c
            (c
                Ephemeral_State ; new ephemeral state
                (list
                    (- total_reserves entry_payout_amount)
                    (- active_shares entry_shares)
                    (c cumulative_payout (+ remaining_rewards payout_rounding_error))
                    Round_Time_Info
                )  ; new state
            )
            (list
                ; manager actually wants us to remove this entry
                (list RECEIVE_MESSAGE
                    18 ; puzzle hash - puzzle hash
                    (concat 'r' (sha256 2
                        (sha256 1 entry_payout_puzzle_hash)
                        (sha256 1 entry_shares)
                    )) ; message = 'r' + (sha265tree (entry_payout_puzzle_hash . entry_shares))
                    (curry_hashes_inline SINGLETON_MOD_HASH
                        MANAGER_SINGLETON_STRUCT_HASH
                        manager_singleton_inner_puzzle_hash
                    )
                )

                ; make sure the reward info is up to date
                (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ last_update MAX_SECONDS_OFFSET))

                ; spend entry reward slot without recreating it
                (spend_slot_inline ENTRY_SLOT_1ST_CURRY_HASH
                    (sha256 2
                        (sha256 1 entry_payout_puzzle_hash)
                        (sha256 2
                            (sha256 1 entry_initial_cumulative_payout)
                            (sha256 1 entry_shares)
                        )
                    ) ; slot value = (payout_puzzle_hash initial_cumulative_payout . shares)
                )

                ; leading -42 -> condition will be returned by the reserve
                (list -42
                    CREATE_COIN
                    entry_payout_puzzle_hash
                    entry_payout_amount
                    (list entry_payout_puzzle_hash)
                )
            ) ; conditions
        )
        ; else
        (x)
    )
)