; unstake.clsp by yakuhito
;; Removes entries from the distributor, returning the underlying asset to the user.

;; Note: Like the stake action, this puzzle has an associated unstaking program that
;;   is set at distributor launch.

(mod (@ curried_args_and_solution (
    ENTRY_SLOT_1ST_CURRY_HASH ; after 1st curry
    MAX_SECONDS_OFFSET ; at most this amount of seconds can pass since last update
    ; /\ this prevents the reward distributor from 'stealing' earned rewards while not
    ;    dropping the remove mirror tx from the mempool after each block
    PRECISION
    UNLOCK_PUZZLE
    (
        Ephemeral_State .
        (@
            Current_State
            (
                total_reserves
                active_shares
                (@ Reward_Info (cumulative_payout . remaining_rewards)) .
                (@ Round_Time_Info (last_update . epoch_end))
            )
        )
    ) ; Truth
    ; solution
    (entry_custody_puzzle_hash entry_initial_cumulative_payout . entry_shares) ; entry slot data
    (entry_payout_amount . payout_rounding_error) . ; payout info
    unlock_puzzle_solution
))
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include curry.clib)
    (include slots.clib)

    (defun main (
        (
            ENTRY_SLOT_1ST_CURRY_HASH
            MAX_SECONDS_OFFSET
            PRECISION
            UNLOCK_PUZZLE
            (
                Ephemeral_State .
                (@
                    Current_State
                    (
                        total_reserves
                        active_shares
                        (@ Reward_Info (cumulative_payout . remaining_rewards)) .
                        (@ Round_Time_Info (last_update . epoch_end))
                    )
                )
            )
            (@ entry_slot_data (entry_custody_puzzle_hash entry_initial_cumulative_payout . entry_shares)) ; entry slot data
            (entry_payout_amount . payout_rounding_error) . ; payout info
            unlock_puzzle_solution
        ) ; curried_args_and_solution
        (shares_to_remove . unlock_conditions)
    )

        (if (all
            (=
                (* shares_to_remove (- cumulative_payout entry_initial_cumulative_payout))
                (+ (* entry_payout_amount PRECISION) payout_rounding_error)
            )
            (> payout_rounding_error -1)
            (> PRECISION payout_rounding_error)
            (not (> shares_to_remove entry_shares))
        )
            (c
                (c
                    Ephemeral_State ; new ephemeral state
                    (c
                        (- total_reserves entry_payout_amount)
                        (c
                            (- active_shares shares_to_remove)
                            (c
                                (c cumulative_payout (+ remaining_rewards payout_rounding_error))
                                Round_Time_Info
                            )
                        )
                    ) ; new state
                )
                (c
                    ; make sure the reward info is up to date
                    (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ last_update MAX_SECONDS_OFFSET))
                    (c
                        ; spend entry reward slot
                        (spend_slot ENTRY_SLOT_1ST_CURRY_HASH
                            (sha256tree entry_slot_data)
                        )
                        (c
                            ; leading -42 -> condition will be returned by the reserve
                            (list -42
                                CREATE_COIN
                                entry_custody_puzzle_hash
                                entry_payout_amount
                                (list entry_custody_puzzle_hash)
                            )
                            (if (= shares_to_remove entry_shares)
                                unlock_conditions
                                ; else
                                (c
                                    ; create new entry reward slot
                                    (create_slot_with_hint ENTRY_SLOT_1ST_CURRY_HASH
                                        (sha256tree
                                            (c 
                                                entry_custody_puzzle_hash
                                                (c
                                                    entry_initial_cumulative_payout
                                                    (- entry_shares shares_to_remove)
                                                )
                                            )
                                        ) ; slot value = (payout_puzzle_hash initial_cumulative_payout . shares)
                                        entry_custody_puzzle_hash
                                    )
                                    unlock_conditions
                                )
                            )
                        )
                    )
                ) ; conditions
            )
            ; else
            (x)
        )
    )

    (main
        curried_args_and_solution
        (a UNLOCK_PUZZLE (c Ephemeral_State (c entry_custody_puzzle_hash unlock_puzzle_solution)))
    )
)